LOAD "testlib.lgo

TO DLLSMOKETEST
  ; Load the windowing subsystem library
  DLLLOAD "user32.dll

  ; Get Handle to Main Window (Note args are in REVERSE order)
  LOCALMAKE "hwnd DLLCALL [l FindWindowA s FMSLogo l 0]

  ; Resize Main Window (should return TRUE)
  SIMPLEREPORTTEST [ DLLCALL (LIST "l "MoveWindow "w "1 "l "100 "l "100 "l "0 "l "0 "l :hwnd) ] [1]

  WAIT 30

  ; Restore Main Window (using captial letters for type specifiers)
  SIMPLEREPORTTEST [ DLLCALL (LIST "L "MoveWindow "W 1 "L 600 "L 800 "L 0 "L 0 "L :hwnd) ] [1]

  DLLFREE
END


TO DLLCALLTEST

  ; call a function when no DLL is loaded
  RUNBADINPUTTEST [DLLCALL [L FindWindowA S FMSLogo L 0]]

  ; Load the User System Kernel
  DLLLOAD "kernel32.dll

  ; invalid calls
  RUNNOTENOUGHINPUTSTEST [(DLLCALL)]
  RUNTOOMANYINPUTSTEST   [(DLLCALL [L GetTickCount] "kernel32.dll "toomany)]

  ; invalid inputs
  RUNSTOPPINGTEST [DLLCALL {0}]

  ; missing the function type
  RUNSTOPPINGTEST [DLLCALL [0]]

  ; missing the function name
  RUNSTOPPINGTEST [DLLCALL [L]]

  ; unknown function
  RUNSTOPPINGTEST [DLLCALL [L FunctionDoesNotExist]]

  ; unknown function type
  RUNSTOPPINGTEST [DLLCALL [E GetTickCount]]

  ; unknown function type
  RUNSTOPPINGTEST [DLLCALL [E GetTickCount]]

  ; type/values are not paired
  RUNSTOPPINGTEST [DLLCALL [L GetTickCount L ]]

  ; unknown parameter type
  RUNSTOPPINGTEST [DLLCALL [L GetTickCount E Unknown]]

  ; wrong number of inputs (DLLCALL may crash on this)
  ; RUNSTOPPINGTEST [DLLCALL [L GetTickCount L 0]]

  ; DLL is not loaded
  RUNSTOPPINGTEST [(DLLCALL [L GetTickCount L 0] "dll-that-is-not-loaded.dll)]

  DLLFREE

END

TO MULTIDLLLOADTEST

  ; load two DLLs at once
  TRACEINSTRUCTION [ DLLLOAD "user32.dll   ]
  TRACEINSTRUCTION [ DLLLOAD "kernel32.dll ]

  ; call into user32.dll
  LOCAL "hwnd
  TRACEINSTRUCTION [ MAKE "hwnd (DLLCALL [l FindWindowA S FMSLogo L 0] "user32.dll) ]

  ; Assuming that a function only exists in one DLL, the DLL name is optional
  SIMPLEREPORTTEST [ DLLCALL [l FindWindowA S FMSLogo L 0] ] :hwnd

  ; Call a function in the other DLL
  TRACEINSTRUCTION [ IGNORE (DLLCALL [L GetTickCount] "kernel32.dll) ]
  TRACEINSTRUCTION [ IGNORE DLLCALL [L GetTickCount] ]

  ; loading an already loaded DLL should simply increment the reference count
  TRACEINSTRUCTION [ DLLLOAD "user32.dll ]
  SIMPLEREPORTTEST [ DLLCALL [l FindWindowA S FMSLogo L 0] ] :hwnd
  TRACEINSTRUCTION [ (DLLFREE "user32.dll) ]

  ; the reference count should still be at one, so the function should still be safe to call
  SIMPLEREPORTTEST [ DLLCALL [l FindWindowA S FMSLogo L 0] ] :hwnd

  ; release the final reference
  TRACEINSTRUCTION [ (DLLFREE "user32.dll) ]

  ; now user32 should be freed, so the next call should fail
  RUNSTOPPINGTEST [ DLLCALL [l FindWindowA S FMSLogo L 0] ] 

  ; now release the reference on kernel32.dll
  TRACEINSTRUCTION [ DLLFREE ]

  ; now user32 should be freed, so the next call should fail
  RUNSTOPPINGTEST [ IGNORE DLLCALL [L GetTickCount] ]

END

TO DLLLOADTEST

  ; wrong number of inputs
  RUNNOTENOUGHINPUTSTEST [(DLLLOAD)]
  RUNTOOMANYINPUTSTEST   [(DLLLOAD "kernel32 "user32)]

  ; invalid filenames
  RUNSTOPPINGTEST [DLLLOAD {0}]
  RUNSTOPPINGTEST [DLLLOAD [0]]
  RUNSTOPPINGTEST [DLLLOAD 12345]
  RUNSTOPPINGTEST [DLLLOAD "nonexistent.dll]
  RUNSTOPPINGTEST [DLLLOAD "c:\\]

  ; DLLLOAD a very long filename name
  localmake "longdllname ".dll
  repeat 5000 [ make "longdllname word "A :longdllname ]
  make "longdllname word "|"| :longdllname
  RUNBADINPUTTEST (list "DLLLOAD :longdllname)

  ; Regression test for bug #1438438 
  ; Call DLLLOAD with invalid input while a DLL is loaded.
  DLLLOAD "kernel32.dll
  RUNSTOPPINGTEST [DLLLOAD {0}]
  DLLFREE

END

TO DLLFREETEST

  ; can't free a DLL when nothing is loaded
  RUNSTOPPINGTEST [DLLFREE]

  ; free a DLL that is not loaded
  RUNSTOPPINGTEST [(DLLFREE "dll-that-is-not-loaded.dll)]

  ; wrong number of inputs
  RUNTOOMANYINPUTSTEST [(DLLFREE "kernel32.dll "toomany)]

END


TO DLLTEST

   ; TODO: test calling a function by ordinal

   DLLSMOKETEST

   DLLLOADTEST
   DLLCALLTEST
   DLLFREETEST

   MULTIDLLLOADTEST
END

