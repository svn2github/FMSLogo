; This is the testharness.
; It provides test primatives.
; It also loads all unit tests and executes them.

LOAD "logic.lgo
LOAD "bitmap.lgo
LOAD "form.lgo
LOAD "editor.lgo
LOAD "port.lgo
LOAD "label.lgo
LOAD "fill.lgo
LOAD "mkdir.lgo


TO REPORTTESTINFO :message
  PRINT (SENTENCE "INFO: :message)
END

TO REPORTTESTPASSED :message
  PRINT (SENTENCE "PASS: :message)
END

TO REPORTTESTFAILED :message
  LOCALMAKE "result (SENTENCE "FAIL: :message)
  PRINT :result
  MESSAGEBOX [Test Failed] :result
END

TO REPORTTEST :testpassed :result :expected
  IFELSE :testpassed [
    REPORTTESTPASSED (sentence :result "|, as expected.|)
  ] [
    REPORTTESTFAILED (sentence :result "|. We expected| :expected)
  ]
END

TO SIMPLEREPORTTEST :instruction :expectedresult
  LOCALMAKE "result RUN :instruction
  IFELSE EQUALP :result :expectedresult [
    REPORTTESTPASSED (sentence :instruction "returned :result "|, as expected.|)
  ] [
    REPORTTESTFAILED (sentence :instruction "returned :result "|. We expected| :expectedresult)
  ]
END


TO REPORTTESTINITIALIZE
  OPENWRITE "testlog.txt
  SETWRITE  "testlog.txt
END


TO REPORTTESTUNINITIALIZE
  SETWRITE []
  CLOSE    "testlog.txt
END


TO RUNBADINPUTTEST :instructions

  REPORTTESTINFO (SENTENCE "|Executing invalid instruction: '| :instructions "' )

  CATCH "ERROR [ 
    IGNORE RUN :instructions 
    REPORTTESTFAILED (SENTENCE "|Executing '| :instructions "|' did not throw an error|)
  ]

END

; Outputs how long it takes to execute given instructions
TO TIMECOMMAND :instructions
  LOCALMAKE "beforetime timemilli

  RUN :instructions

  LOCALMAKE "aftertime timemilli

  OUTPUT :aftertime - :beforetime
END


TO TESTALL

  REPORTTESTINITIALIZE

  LOGICTEST  ; from logic.lgo
  BITMAPTEST ; from bitmap.lgo
  FORMTEST   ; from form.lgo
  EDITORTEST ; from editor.lgo 
  PORTTEST   ; from port.lgo
  LABELTEST  ; from label.lgo
  FILLTEST   ; from fill.lgo
;  MKDIRTEST  ; from mkdir.lgo

  REPORTTESTUNINITIALIZE
END

; execute all tests on startup
make "startup [TESTALL]

