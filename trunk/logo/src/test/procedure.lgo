LOAD "testlib.lgo

TO SAMPLEPROC  :a  :b
  PRINT :a ; print
  SHOW  :b ; show
END

; Creates a local variable named varname (which must be a string).
; It assigns a value to the variable,
; then outputs the value of the variable.
TO LOCALVARTEST :varname :value

  ; create the local variable
  LOCAL :varname

  ; assign it a value
  MAKE  :varname :value

  ; return the value of :varname (evaluated twice)
  OUTPUT RUN (WORD ": :varname)
END

TO PROCEDURETEST

  ; test TEXT
  SIMPLEREPORTTEST ~
    [TEXT "SAMPLEPROC] ~
    [[a b] [PRINT :a] [SHOW :b]]

  RUNBADINPUTTEST [(TEXT)]
  RUNBADINPUTTEST [(TEXT "nonexistent)]
  RUNBADINPUTTEST [(TEXT [sampleproc])]
  RUNBADINPUTTEST [(TEXT {sampleproc})]

  ; test FULLTEXT
  SIMPLEREPORTTEST ~
     [FULLTEXT "SAMPLEPROC] ~
     (LIST 
        "|TO SAMPLEPROC  :a  :b|
        "|  PRINT :a ; print| 
        "|  SHOW  :b ; show| 
        "END)

   RUNBADINPUTTEST [(FULLTEXT)]
   RUNBADINPUTTEST [(FULLTEXT "nonexistent)]
   RUNBADINPUTTEST [(FULLTEXT [sampleproc])]
   RUNBADINPUTTEST [(FULLTEXT {sampleproc})]

   ; test DEFINEDP
   SIMPLEREPORTTEST [DEFINEDP "nonexistent] "false
   SIMPLEREPORTTEST [DEFINED? "nonexistent] "false
   SIMPLEREPORTTEST [DEFINED? "FORWARD]     "false
   SIMPLEREPORTTEST [DEFINED? "SAMPLEPROC]  "true
   SIMPLEREPORTTEST [DEFINED? "sampleproc]  "true
   SIMPLEREPORTTEST [DEFINED? "||]          "false
   RUNBADINPUTTEST [(DEFINED?)]
   RUNBADINPUTTEST [(DEFINED? {a})]
   RUNBADINPUTTEST [(DEFINED? [])]
   RUNBADINPUTTEST [(DEFINED? "sampleproc "toomany)]

   ; test PROCEDUREP
   SIMPLEREPORTTEST [PROCEDUREP "nonexistent] "false
   SIMPLEREPORTTEST [PROCEDURE? "nonexistent] "false
   SIMPLEREPORTTEST [PROCEDURE? "FORWARD]     "true
   SIMPLEREPORTTEST [PROCEDURE? "SAMPLEPROC]  "true
   SIMPLEREPORTTEST [PROCEDURE? "sampleproc]  "true
   SIMPLEREPORTTEST [PROCEDURE? "||]          "false
   RUNBADINPUTTEST [(PROCEDURE?)]
   RUNBADINPUTTEST [(PROCEDURE? {a})]
   RUNBADINPUTTEST [(PROCEDURE? [])]
   RUNBADINPUTTEST [(PROCEDURE? "sampleproc "toomany)]

   ; test NAMEP
   LOCALMAKE "namepvariable 0
   SIMPLEREPORTTEST [NAMEP "nonexistent]   "false
   SIMPLEREPORTTEST [NAME? "nonexistent]   "false
   SIMPLEREPORTTEST [NAME? "FORWARD]       "false
   SIMPLEREPORTTEST [NAME? "||]            "false
   SIMPLEREPORTTEST [NAME? "namepvariable] "true
   SIMPLEREPORTTEST [NAME? "NAMEPVARIABLE] "true
   RUNBADINPUTTEST [(NAME?)]
   RUNBADINPUTTEST [(NAME? {a})]
   RUNBADINPUTTEST [(NAME? [])]
   RUNBADINPUTTEST [(NAME? "namepvariable "toomany)]

   ; test PRIMITIVEP
   SIMPLEREPORTTEST [PRIMITIVEP "nonexistent]   "false
   SIMPLEREPORTTEST [PRIMITIVE? "nonexistent]   "false
   SIMPLEREPORTTEST [PRIMITIVE? "FORWARD]       "true
   SIMPLEREPORTTEST [PRIMITIVE? "FD]            "true
   SIMPLEREPORTTEST [PRIMITIVE? "||]            "false
   SIMPLEREPORTTEST [PRIMITIVE? "namepvariable] "false
   SIMPLEREPORTTEST [PRIMITIVE? "sampleproc]    "false
   RUNBADINPUTTEST [(PRIMITIVE?)]
   RUNBADINPUTTEST [(PRIMITIVE? {a})]
   RUNBADINPUTTEST [(PRIMITIVE? [])]
   RUNBADINPUTTEST [(PRIMITIVE? "namepvariable "toomany)]

   ; test MACROP
   .DEFMACRO "mymacro [[a b] [print :a] [print :b]]
   SIMPLEREPORTTEST [MACROP "mymacro]       "true
   SIMPLEREPORTTEST [MACRO? "mymacro]       "true
   SIMPLEREPORTTEST [MACRO? "FORWARD]       "false
   SIMPLEREPORTTEST [MACRO? "FD]            "false
   SIMPLEREPORTTEST [MACRO? "||]            "false
   SIMPLEREPORTTEST [MACRO? "namepvariable] "false
   SIMPLEREPORTTEST [MACRO? "sampleproc]    "false
   RUNBADINPUTTEST [(MACRO?)]
   RUNBADINPUTTEST [(MACRO? {a})]
   RUNBADINPUTTEST [(MACRO? [])]
   RUNBADINPUTTEST [(MACRO? "mymacro "toomany)]

   ; test DEFINE
   DEFINE "ECHO [[a] [output :a] ]
   SIMPLEREPORTTEST [ECHO "input] "input
   SIMPLEREPORTTEST [DEFINEDP "ECHO] "true
   SIMPLEREPORTTEST [DEFINED? "ECHO] "true
   RUNBADINPUTTEST [(DEFINE)]
   RUNBADINPUTTEST [(DEFINE "function)]
   RUNBADINPUTTEST [(DEFINE "function [])]

   ; test LOCAL
   SIMPLEREPORTTEST [LOCALVARTEST "unknownlocalvar 42] 42
   SIMPLEREPORTTEST [NAME? "unknownlocalvar] "false

   ; silly, but legal
   LOCAL [10 11]
   MAKE "10 "ten
   MAKE "11 "eleven
   SIMPLEREPORTTEST [:10] "ten
   SIMPLEREPORTTEST [:11] "eleven

   (LOCAL "12 "13)
   MAKE "12 "twelve
   MAKE "13 "eleven
   SIMPLEREPORTTEST [:10] "ten
   SIMPLEREPORTTEST [:11] "eleven

   LOCAL []
   RUNBADINPUTTEST [(LOCAL)]
   RUNBADINPUTTEST [LOCAL {a}]

   ; test ERASE
   DEFINE "ERASEDPROC [[] [] ]
   SIMPLEREPORTTEST [PROCEDURE? "erasedproc] "true
   ERASE "ERASEDPROC
   SIMPLEREPORTTEST [PROCEDURE? "erasedproc] "false
   RUNBADINPUTTEST [ERASEDPROC]
   RUNBADINPUTTEST [(ERASE)]
   RUNBADINPUTTEST [ERASE {a}]
   RUNBADINPUTTEST [ERASE "forward]

   ; test ERALL
   ; We can't run our tests if we erase all procedures, 
   ; so we just test for not erasing anything and bad input.
   MESSAGEBOX "|Manual Test| "|You will be now asked to erase all procedures.  Say No.|
   ERALL
   SIMPLEREPORTTEST [PROCEDURE? "sampleproc]  "true
   RUNBADINPUTTEST [(ERALL 10)]
   RUNBADINPUTTEST [(ERALL [])]
   RUNBADINPUTTEST [(ERALL {a})]
   RUNBADINPUTTEST [(ERALL "no)]


   ; test LOCALMAKE
   LOCALMAKE "mylocalvarname 32
   SIMPLEREPORTTEST [LOCALVARTEST "mylocalvarname 42]  42
   SIMPLEREPORTTEST [:mylocalvarname]                  32
   RUNBADINPUTTEST [(LOCALMAKE)]

   ; Just exercise CONTENTS for now.
   ; we'll figure out how to test its output later
   IGNORE CONTENTS

   ; Just exercise NAMES for now.
   ; we'll figure out how to test its output later
   IGNORE NAMES

   ; Just exercise BURIED for now.
   ; we'll figure out how to test its output later
   IGNORE BURIED

   ; Just exercise PROCEDURES for now.
   ; we'll figure out how to test its output later
   IGNORE PROCEDURES

   ; test NAMELIST
   SIMPLEREPORTTEST [NAMELIST "a]     [[] [a]]
   SIMPLEREPORTTEST [NAMELIST [a b]]  [[] [a b]]
   SIMPLEREPORTTEST [NAMELIST "||]    (list [] (list "||))
   SIMPLEREPORTTEST [NAMELIST []]     [[] []]
   RUNBADINPUTTEST [(NAMELIST)]
   RUNBADINPUTTEST [NAMELIST {a b}]
   RUNBADINPUTTEST [(NAMELIST "a "b)]

   ; Just exercise the PO routines for now.
   ; we'll figure out how to test its output later
   OPENWRITE "po.dump.txt
   LOCALMAKE "previouswriter WRITER
   SETWRITE "po.dump.txt

   PO CONTENTS
   PO NAMES
   PO PROCEDURES
   PO BURIED
   PO PLISTS
   PO []

   POALL
   POPS
   PONS
   POPLS

   PON "namepvariable
   PON [namepvariable]

   ; create some property lists for testing POPL
   PPROP "wordtonumber "one 1
   PPROP "wordtonumber "two 2
   PPROP "numbertoword 1 "one
   PPROP "numbertoword 2 "two
   POPL "wordtonumber
   POPL "numbertoword
   POPL [wordtonumber numbertoword nonexistent]

   ; remove the property lists
   REMPROP "wordtonumber "one
   REMPROP "wordtonumber "two
   REMPROP "numbertoword 1
   REMPROP "numbertoword 2
   POPL "wordtonumber
   POPL "numbertoword
   POPL [wordtonumber numbertoword nonexistent]

   POT CONTENTS
   POT NAMES
   POT PROCEDURES
   POT BURIED
   POT PLISTS
   POT []

   POTS

   SETWRITE :previouswriter
   CLOSE "po.dump.txt
   ERASEFILE "po.dump.txt

   RUNBADINPUTTEST [(PO)]
   RUNBADINPUTTEST [(PO "pot "pots)]
   RUNBADINPUTTEST [PO "forward]
   RUNBADINPUTTEST [PO "nonexistent]
   RUNBADINPUTTEST [PO [nonexistent]]
   RUNBADINPUTTEST [PO {a}]

   RUNBADINPUTTEST [(POALL "toomany)]
   RUNBADINPUTTEST [(POPS  "toomany)]
   RUNBADINPUTTEST [(POPLS "toomany)]
   RUNBADINPUTTEST [(POT   "toomany)]
   RUNBADINPUTTEST [(POTS  "toomany)]

   RUNBADINPUTTEST [(PON)]
   RUNBADINPUTTEST [PON "sampleproc]
   RUNBADINPUTTEST [(PON "too "many)]
   RUNBADINPUTTEST [PON {a b}]

   RUNBADINPUTTEST [(POT)]
   RUNBADINPUTTEST [(POT "pot "pots)]
   RUNBADINPUTTEST [POT "forward]
   RUNBADINPUTTEST [POT "nonexistent]
   RUNBADINPUTTEST [POT [nonexistent]]
   RUNBADINPUTTEST [POT {a}]
END

