LOAD "testlib.lgo
LOAD "uiautomationlib.lgo

TO SETPENSIZEDIALOGTEST.BUG3418522

   UIAUTOMATION.INITIALIZE

   CATCH "ERROR [
     ; Taken from logorc.h (OWL)
     LOCALMAKE "CMD_BASE      32771
     LOCALMAKE "CM_SETPENSIZE :CMD_BASE + 55

     ; For wx version
     LOCALMAKE "wxID_HIGHEST  5999               ; from defs.h
     LOCALMAKE "ID_SETPENSIZE :wxID_HIGHEST + 15 ; mainframe.cpp

     REPEAT 2 [
       ; Use the "Set Pen Size" menu command to open the Pen Size dialog box
       localmake "message IFELSE ISWX [ :ID_SETPENSIZE ][ :CM_SETPENSIZE ]
       UIAUTOMATION.SENDMENUCOMMANDTOWINDOW "FMSLogo :message

       localmake "WM_VSCROLL  277
       localmake "SB_LINEDOWN 3
       localmake "hwnd        UIAUTOMATION.FINDWINDOW [Pen Size] 0
       localmake "sliderclass IFELSE ISWX ["msctls_trackbar32] ["SizeControl]
       UIAUTOMATION.SENDMESSAGETOWINDOW [Pen Size] :sliderclass :WM_VSCROLL :SB_LINEDOWN :hwnd

       ; dismiss the dialog box
       UIAUTOMATION.SENDBUTTONPUSHTOWINDOW [Pen Size] "Cancel
     ]
   ]

   IF NOT FAILIFERROR "SETPENSIZEDIALOGTEST.BUG3418522 [
     REPORTTESTPASSED "|Bug #3418522 is fixed; Sending a vertical scroll message to the Pen Size dialog box did not crash.|
   ]

   UIAUTOMATION.UNINITIALIZE

END

TO SETPENSIZEDIALOGTEST
  SETPENSIZEDIALOGTEST.BUG3418522
END
